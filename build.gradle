plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.2'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.umc.apiwiki'
version = '0.0.1-SNAPSHOT'

// 자바 21 설정
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // 1. 기본 웹 + JPA
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation' // 유효성 검사(@NotNull 등)
    implementation 'io.sentry:sentry-spring-boot-starter:7.3.0'
    implementation 'io.sentry:sentry-logback:7.3.0'

    // 2. DB 관련
    runtimeOnly 'com.mysql:mysql-connector-j' // 실제 서버용 (MySQL)
    runtimeOnly 'com.h2database:h2'           // 테스트, 로컬용 (H2)

    // 3. 롬복 (Lombok)
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // 4. 인증/인가 (Security + JWT)
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.5'

    // 5. QueryDSL (스프링 부트 3.x 이상은 Jakarta 버전 필수) - OpenFeign
    implementation "io.github.openfeign.querydsl:querydsl-jpa:7.0"
    implementation "io.github.openfeign.querydsl:querydsl-core:7.0"
    annotationProcessor "io.github.openfeign.querydsl:querydsl-apt:7.0:jpa"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"

    // 6. Swagger (SpringDoc OpenAPI - 스프링 부트 3.4와 호완을 위해 2.7 이상을 사용)
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.3'

    // 7. 테스트
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test' // 시큐리티 테스트용
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

// QueryDSL 관련 설정
// generated/querydsl 폴더 생성 & 삽입
def querydslDir = layout.buildDirectory.dir("generated/querydsl").get().asFile

// 소스 세트에 생성 경로 추가 (구체적인 경로 지정)
sourceSets {
    main.java.srcDirs += [ querydslDir ]
}

// 컴파일 시 생성 경로 지정
tasks.withType(JavaCompile).configureEach {
    options.generatedSourceOutputDirectory.set(querydslDir)
}

// clean 태스크에 생성 폴더 삭제 로직 추가
clean.doLast {
    file(querydslDir).deleteDir()
}